apply plugin: 'com.android.application'
apply plugin: 'bugly'

Properties properties = new Properties()
properties.load(project.file('configs/config.properties').newDataInputStream())

bugly {
    appId = loadBuildProperties('release').getProperty("BUGLY_APPID")
    appKey = loadBuildProperties('release').getProperty("BUGLY_APPKEY")
}

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode properties.getProperty("VERSION_CODE").toInteger()
        versionName versionName(properties)
        applicationId "com.jiaye.cashloan"
        buildConfigField("int", "DB_VERSION", properties.getProperty("DB_VERSION"))
        buildConfigField("String", "PUBLIC_KEY", properties.getProperty("PUBLIC_KEY"))
        buildConfigField("String", "TONGDUN_URL", properties.getProperty("TONGDUN_URL"))
        buildConfigField("String", "TONGDUN_CODE", properties.getProperty("TONGDUN_CODE"))
        buildConfigField("String", "TONGDUN_KEY", properties.getProperty("TONGDUN_KEY"))
        buildConfigField("String", "TONGDUN_APP_NAME", properties.getProperty("TONGDUN_APP_NAME"))
        buildConfigField("String", "OSS_ENDPOINT", properties.getProperty("OSS_ENDPOINT"))
        buildConfigField("String", "OSS_ACCESS_KEY_ID", properties.getProperty("OSS_ACCESS_KEY_ID"))
        buildConfigField("String", "OSS_ACCESS_KEY_SECRET", properties.getProperty("OSS_ACCESS_KEY_SECRET"))
        buildConfigField("String", "OSS_BUCKET_NAME", properties.getProperty("OSS_BUCKET_NAME"))
        buildConfigField("String", "OSS_BASE_URL", properties.getProperty("OSS_BASE_URL"))
        buildConfigField("String", "MOXIE_URL", properties.getProperty("MOXIE_URL"))
        buildConfigField("String", "MOXIE_APIKEY", properties.getProperty("MOXIE_APIKEY"))
        buildConfigField("String", "MOXIE_TOKEN", properties.getProperty("MOXIE_TOKEN"))
    }
    /**
     * url  对应服务器地址
     */
    flavorDimensions "url"
    productFlavors {
        urlDev {
            dimension "url"
            flavorBuildConfigField(urlDev, loadFlavorProperties('dev'))
        }
        urlTest {
            dimension "url"
            flavorBuildConfigField(urlTest, loadFlavorProperties('test'))
        }
        urlProduct {
            dimension "url"
            flavorBuildConfigField(urlProduct, loadFlavorProperties('product'))
        }
    }
    signingConfigs {
        debug {
            keyAlias 'loan'
            keyPassword 'com.jiaye.cashloan'
            storeFile file('../key')
            storePassword 'com.jiaye.cashloan'
        }
        release {
            keyAlias 'loan'
            keyPassword 'com.jiaye.cashloan'
            storeFile file('../key')
            storePassword 'com.jiaye.cashloan'
        }
    }
    buildTypes {
        debug {
            buildBuildConfigField(debug, loadBuildProperties('debug'))
            buildManifestPlaceholders(debug, loadBuildProperties('debug'))
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            buildBuildConfigField(release, loadBuildProperties('release'))
            buildManifestPlaceholders(release, loadBuildProperties('release'))
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    android.applicationVariants.all { variant ->
        renameApk(variant)
    }
    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.android.support:appcompat-v7:26.1.0'
    api 'com.android.support:recyclerview-v7:26.1.0'
    api 'com.android.support:design:26.1.0'
    // https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxjava
    api 'io.reactivex.rxjava2:rxjava:2.1.4'
    // https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxandroid
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-gson
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    // https://mvnrepository.com/artifact/com.squareup.retrofit2/adapter-rxjava2
    api 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    api 'com.squareup.okhttp3:okhttp:3.9.0'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor
    api 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    // https://github.com/orhanobut/logger
    api 'com.orhanobut:logger:2.2.0'
    // https://github.com/Bigkoo/Android-PickerView
    api 'com.contrarywind:Android-PickerView:3.2.7'
    // bugly
    api 'com.tencent.bugly:crashreport:2.6.6.1'
    // https://github.com/zetbaitsu/Compressor
    api 'id.zelory:compressor:1.0.5'
    // http://lbs.amap.com/api/android-location-sdk/guide/android-location/getlocation
    api 'com.amap.api:location:4.0.1'
    // https://help.aliyun.com/document_detail/32043.html
    api 'com.aliyun.dpa:oss-android-sdk:2.8.3'
    api 'com.squareup.okio:okio:1.13.0'
    // http://open.51datakey.com/
    api 'com.moxie:moxie-client:2.3.5'

    // module
    implementation project(':tongdun')
    // https://github.com/crazycodeboy/TakePhoto
    implementation project(':takephoto')
    // https://github.com/googlesamples/easypermissions
    implementation project(':easypermissions')
}

static def versionName(properties) {
    return properties.getProperty("MILESTONE_CODE") + "." + properties.getProperty("CODE") + "." + properties.getProperty("FIX_CODE") + "." + properties.getProperty("VERSION_CODE")
}

def loadFlavorProperties(path) {
    Properties properties = new Properties()
    properties.load(project.file('configs/url/' + path + '/config.properties').newDataInputStream())
    return properties
}

def loadBuildProperties(path) {
    Properties properties = new Properties()
    properties.load(project.file('configs/build/' + path + '/config.properties').newDataInputStream())
    return properties
}

static def flavorBuildConfigField(flavor, properties) {
    flavor.buildConfigField("String", "BASE_URL", properties.getProperty("BASE_URL"))
    flavor.buildConfigField("String", "BASE_SHARE_URL", properties.getProperty("BASE_SHARE_URL"))
    flavor.buildConfigField("String", "SATCATCHE_URL", properties.getProperty("SATCATCHE_URL"))
    flavor.buildConfigField("String", "CREDIT2GO_URL", properties.getProperty("CREDIT2GO_URL"))
    flavor.buildConfigField("String", "CREDIT2GO_ESCROW_URL", properties.getProperty("CREDIT2GO_ESCROW_URL"))
}

static def buildBuildConfigField(build, properties) {
    build.buildConfigField("String", "BUGLY_APPID", properties.getProperty("BUGLY_APPID"))
}

static def buildManifestPlaceholders(build, properties) {
    build.manifestPlaceholders = [AMAP_APIKEY_NAME: properties.getProperty("AMAP_APIKEY_NAME"), AMAP_APIKEY_VALUE: properties.getProperty("AMAP_APIKEY_VALUE")]
}

static def renameApk(variant) {
    variant.outputs.all {
        String fileName = outputFileName.toString()
        outputFileName = fileName.substring(0, fileName.length() - 4) + "-" + "${variant.versionName}.apk"
    }
}
